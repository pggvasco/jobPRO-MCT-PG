Public Class RenevueOneLinerToErp
    Inherits ShopFloorTrigger(Of API.Job)
    Private _jobcard As Pastel.Evolution.JobCard
    Private _CancelJobStage As Boolean = False


    Protected Friend Property JobCard As Pastel.Evolution.JobCard
        Get
            If _jobcard Is Nothing And Me.Instance.JobCardID.HasValue Then
                _jobcard = New Pastel.Evolution.JobCard(Instance.JobCardID.Value)
            End If
            Return _jobcard
        End Get
        Set(ByVal value As Pastel.Evolution.JobCard)
            _jobcard = value
            If value IsNot Nothing AndAlso value.ID > 0 Then
                Instance.JobCardID = value.ID
            End If
        End Set
    End Property

    Protected Friend Property ERPRequisitionLine As Pastel.Evolution.JobDetail

    Public Sub CancelJobStage()
        _CancelJobStage = True
    End Sub


    Public Sub New(ByVal job As API.Job)
        MyBase.New(job)
    End Sub

    Protected Overrides Sub Run()
        Try
            'Threading.Thread.Sleep(5000)
            Add(String.Format("Sending Job({0}) Renevue One Liner to ERP...", Instance.JobNumber))
            SendToErp()
            Add("Renevue One Liner sent to ERP.")
        Catch ex As Exception
            Add("Error sending Renevue One Liner To Erp", ex)
            If ex.InnerException IsNot Nothing Then
                Add("InnerException: ", ex.InnerException)
            End If
            RollbackTran = True
        End Try

    End Sub

    Public Overridable Sub SendToErp()

        If API.Context.Defaults.IsRevenueOneLinerAutoGenerated <> True Then Exit Sub
        If API.Context.Defaults.RevenueMethod <> API.RevenueMethod.OneLiner AndAlso API.Context.Defaults.RevenueMethod <> API.RevenueMethod.JobTypeDefined Then Exit Sub
        If API.Context.Defaults.RevenueMethod = API.RevenueMethod.JobTypeDefined AndAlso Instance.JobType.RevenueMethod <> API.RevenueMethod.OneLiner Then Exit Sub

        If Instance.JobCardID <= 0 Then
            Throw New Exception("ERP JobCard has not been created.")
        End If

        If Instance.JobCardID Is Nothing AndAlso _CancelJobStage Then
            Instance.IsApproved = True
            Dim xftr As New xfShopFloorTriggerRunner(Of API.Job)
            xftr.Run(Instance, API.Trigger.JobToErp)
            xftr.ShowDialog()
        End If

        JobCard = New Pastel.Evolution.JobCard(CType(Instance.JobCardID, Integer))
        If JobCard.Status = 2 Then ' Pastel.Evolution.JobCard.JobStatus.Complete Then
            'Throw New Exception(String.Format("ERP JobCard {0} Not Active.", Instance.JobNumber))
            Return
        End If
        Dim IsOneLinerItemFound As Boolean = False
        For Each JobCardDetail As Pastel.Evolution.JobDetail In JobCard.Detail
            If JobCardDetail.InventoryItemID = API.Context.Defaults.RevenueOneLinerInventoryItemID Then
                IsOneLinerItemFound = True
                Exit For
            End If
        Next

        If IsOneLinerItemFound = False Then
            SendRevenueOneLinerToERP()
        End If

    End Sub

    Public Overridable Sub SendRevenueOneLinerToERP()

        Me.ERPRequisitionLine = New Pastel.Evolution.JobDetail
        With Me.ERPRequisitionLine
            Try
                .StartDate = Instance.JobDate
            Catch ex As Exception

            End Try
            .TransactionCode = New Pastel.Evolution.JobTransactionCode(Pastel.Evolution.JobDetail.TransactionSource.Inventory, Instance.CostCentre.Code)
            .InventoryItemID = API.Context.Defaults.RevenueOneLinerInventoryItemID
            If Not String.IsNullOrEmpty(API.Context.Defaults.ManufacturingWarehouse) AndAlso .InventoryItem.IsWarehouseTracked Then
                .Warehouse = New Pastel.Evolution.Warehouse(API.Context.Defaults.ManufacturingWarehouse)
            End If
            .DiscountPercent = 0
            If _CancelJobStage = True Then
                .UnitSellingPrice = 0
            Else
                .UnitSellingPrice = Instance.TotalPrice
            End If
            If Instance.Description1 IsNot Nothing Then
                If Instance.Description1.Length > 40 Then
                    .Description = Instance.Description1.Substring(0, 40)
                Else
                    .Description = Instance.Description1
                End If
            Else
                .Description = ""
            End If
            .Quantity = Instance.Quantity
            If API.Context.Defaults.JobCardCreateBudgetLines Then
                .BudgetUnitCostPrice = 0
                .BudgetUnitSellingPrice = 0
            End If

            'TODO: Checks and sets Vat
            Dim PastelClient As Pastel.Evolution.Customer = New Pastel.Evolution.Customer(CType(Instance.Customer.ID, Integer))
            If PastelClient.ChargeTax = False Then
                Dim jobDefaults As Pastel.Evolution.Defaults = New Pastel.Evolution.Defaults
                .SalesTaxRate = jobDefaults.DebtorsZeroTaxRate
                .PurchasesTaxRate = jobDefaults.DebtorsZeroTaxRate
            End If
            .Status = Pastel.Evolution.JobCard.JobStatus.Active
        End With

        JobCard.Detail.Add(ERPRequisitionLine)
        Try
            JobCard.Save()
        Catch ex As Exception
            'Instance.Delete()
            Throw New Exception(ex.Message)
        End Try
    End Sub

    Protected Overrides Sub RunReport()

    End Sub

End Class
